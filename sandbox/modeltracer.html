<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01//EN"
	 "http://www.w3.org/TR/html4/strict.dtd">

<html>

<head>

	<meta http-equiv="content-type" content="text/html;charset=utf-8">
	<link rel="stylesheet" type="text/css" href="../style/style.css">
	<script type="text/javascript" src="../include/jsmodeler.js"></script>
	<script type="text/javascript" src="../include/gputracer.js"></script>
	<script type="text/javascript" src="../include/userinterface.js"></script>
	<script type="text/javascript" src="modeltracer.js"></script>
	<title>General Purpose WebGL Path Tracer</title>
	
	<script id="fragmentshader" type="x-shader/fragment">
		precision highp float;

		varying vec2 vVertexPosition;
		uniform float uSize;

		#define EPS 0.0000005
		#define BIGEPS 0.005
		#define PI 3.1415926535897932384626433832795
		#define INF 1.0 / 0.0

		uniform sampler2D uOriginalTextureSampler;
		uniform sampler2D uTriangleTextureSampler;
		uniform float uTriangleTextureSize;
		uniform sampler2D uMaterialTextureSampler;
		uniform float uMaterialTextureSize;

		uniform vec3 uCameraData[3];
		uniform vec3 uLightPosition;
		uniform float uLightRadius;
		uniform float uIteration;
		uniform int uPreview;

		struct Triangle
		{
			vec3 v0;
			vec3 v1;
			vec3 v2;
			vec3 n0;
			vec3 n1;
			vec3 n2;
			int mat;
		};

		struct Material
		{
			vec3 diffuse;
			float reflection;
		};

		struct Camera
		{
			vec3 eye;
			vec3 center;
			vec3 up;
		};

		struct Ray
		{
			vec3 origin;
			vec3 direction;
			float length;
		};

		struct Intersection
		{
			vec3 position;
			float distance;
			vec3 normal;
			int material;
		};

		float Random (vec2 seed)
		{
			vec2 theSeed = seed + vec2 (uIteration, -uIteration);
			return fract (sin (dot (theSeed, vec2 (12.9898,78.233))) * 43758.5453);
		}

		vec3 CalculateTriangleNormal (in Triangle triangle)
		{
			vec3 v = triangle.v1 - triangle.v0;
			vec3 w = triangle.v2 - triangle.v0;
			
			vec3 normal;
			normal.x = (v.y * w.z - v.z * w.y);
			normal.y = (v.z * w.x - v.x * w.z);
			normal.z = (v.x * w.y - v.y * w.x);

			return normalize (normal);
		}

		float GetTriangleArea (in float a, in float b, in float c)
		{
			float s = (a + b + c) / 2.0;
			float areaSquare = s * (s - a) * (s - b) * (s - c);
			if (areaSquare < EPS) {
				return 0.0;
			}
			return sqrt (areaSquare);
		}

		vec3 BarycentricInterpolation (in vec3 vertex0, in vec3 vertex1, in vec3 vertex2, in vec3 value0, in vec3 value1, in vec3 value2, in vec3 position)
		{
			float edge0 = length (vertex0 - vertex1);
			float edge1 = length (vertex1 - vertex2);
			float edge2 = length (vertex2 - vertex0);
			
			float distance0 = length (vertex0 - position);
			float distance1 = length (vertex1 - position);
			float distance2 = length (vertex2 - position);
			
			float area = GetTriangleArea (edge0, edge1, edge2);
			if (area == 0.0) {
				return value0;
			}
			
			float area0 = GetTriangleArea (edge0, distance0, distance1);
			float area1 = GetTriangleArea (edge1, distance1, distance2);
			float area2 = GetTriangleArea (edge2, distance0, distance2);
			
			vec3 interpolated0 = value0 * area1;
			vec3 interpolated1 = value1 * area2;
			vec3 interpolated2 = value2 * area0;
			vec3 interpolated = interpolated0 + interpolated1 + interpolated2;
			return interpolated / area;
		}

		vec3 GetTriangleNormal (in Triangle triangle, in vec3 normalPosition)
		{
			vec3 normal = BarycentricInterpolation (
				triangle.v0, triangle.v1, triangle.v2,
				triangle.n0, triangle.n1, triangle.n2,
				normalPosition);
			return normalize (normal);
		}

		bool RayTriangleIntersection (in Ray ray, in vec3 v0, in vec3 v1, in vec3 v2, bool frontFacing, out Intersection intersection)
		{
			vec3 rayOrigin = ray.origin;
			vec3 rayDirection = ray.direction;

			vec3 edgeDir1 = v1 - v0;
			vec3 edgeDir2 = v2 - v0;
			vec3 pVector = cross (rayDirection, edgeDir2);

			float determinant = dot (edgeDir1, pVector);
			if (abs (determinant) < EPS) {
				return false;
			}
			
			bool isFrontFacing = determinant > 0.0;
			if (frontFacing && !isFrontFacing) {
				return false;
			}

			float invDeterminant = 1.0 / determinant;

			vec3 tVector = rayOrigin - v0;
			float u = dot (tVector, pVector) * invDeterminant;
			if (u < -EPS || u > 1.0 - EPS) {
				return false;
			}

			vec3 qVector = cross (tVector, edgeDir1);
			float v = dot (rayDirection, qVector) * invDeterminant;
			if (v < -EPS || u + v > 1.0 - EPS) {
				return false;
			}
		 
			float distance = dot (edgeDir2, qVector) * invDeterminant;
			if (distance < EPS) {
				return false;
			}

			if (ray.length < distance) {
				return false;
			}
			
			intersection.position = rayOrigin + rayDirection * distance;
			intersection.distance = distance;
			intersection.material = -1;
			return true;
		}

		vec3 GetReflectedDirection (in vec3 direction, in vec3 normal)
		{
			float dotProduct = dot (normal, direction);
			vec3 reflected = direction - normal * 2.0 * dotProduct;
			return normalize (reflected);
		}

		vec3 RandomDirOnSphere (in vec2 seed)
		{
			vec2 random = vec2 (Random (seed), Random (seed * 2.0)) * 6.283;
			vec3 randomDir = vec3 (sin (random.x) * vec2 (sin (random.y), cos (random.y)), cos (random.x));
			return normalize (randomDir);
		}

		vec3 RandomDirOnHemisphere (in vec3 normal, in vec2 seed)
		{
			vec3 randomDir = RandomDirOnSphere (seed);
			return (dot (randomDir, normal) < 0.0) ? -randomDir : randomDir;
		}

		vec3 GetFloatVector (in sampler2D sampler, in float textureSize, in int index)
		{
			float pixelSize = 1.0 / textureSize;
			float xCoordinate = mod (float (index), textureSize) * pixelSize + pixelSize * 0.5;
			float yCoordinate = floor (float (index) / textureSize) * pixelSize + pixelSize * 0.5;
			vec4 textureColor = texture2D (sampler, vec2 (xCoordinate, yCoordinate));
			return textureColor.xyz;
		}

		Triangle GetTriangle (in int index)
		{
			Triangle triangle;
			triangle.v0 = GetFloatVector (uTriangleTextureSampler, uTriangleTextureSize, index * 7 + 0);
			triangle.v1 = GetFloatVector (uTriangleTextureSampler, uTriangleTextureSize, index * 7 + 1);
			triangle.v2 = GetFloatVector (uTriangleTextureSampler, uTriangleTextureSize, index * 7 + 2);
			triangle.n0 = GetFloatVector (uTriangleTextureSampler, uTriangleTextureSize, index * 7 + 3);
			triangle.n1 = GetFloatVector (uTriangleTextureSampler, uTriangleTextureSize, index * 7 + 4);
			triangle.n2 = GetFloatVector (uTriangleTextureSampler, uTriangleTextureSize, index * 7 + 5);
			vec3 vec = GetFloatVector (uTriangleTextureSampler, uTriangleTextureSize, index * 7 + 6);
			triangle.mat = int (vec.x);
			return triangle;
		}

		Material GetMaterial (in int index)
		{
			Material material;
			material.diffuse = GetFloatVector (uMaterialTextureSampler, uMaterialTextureSize, index * 2 + 0);
			vec3 vec = GetFloatVector (uMaterialTextureSampler, uMaterialTextureSize, index * 2 + 1);
			material.reflection = vec.x;
			return material;
		}

		vec3 PhongShading (in vec3 shadedPoint, in vec3 shadedPointNormal, in vec3 lightPosition, in Material shadedMaterial)
		{
			float lightDiffuseIntensity = 1.0;
			vec3 lightDirection = normalize (lightPosition - shadedPoint);
			float diffuseCoeff = max (dot (lightDirection, shadedPointNormal), 0.0);
			vec3 diffuseColor = shadedMaterial.diffuse * lightDiffuseIntensity;
			return diffuseColor * diffuseCoeff;
		}

		Intersection RayModelIntersection (in Ray ray, bool frontFacing)
		{
			Intersection result;
			result.distance = INF;
			int foundTriangle = -1;
			for (int i = 0; i < TRIANGLE_COUNT; i++) {
				Triangle triangle = GetTriangle (i);
				Intersection currentIntersection;
				if (RayTriangleIntersection (ray, triangle.v0, triangle.v1, triangle.v2, frontFacing, currentIntersection)) {
					if (currentIntersection.distance < result.distance) {
						result.position = currentIntersection.position;
						result.distance = currentIntersection.distance;
						foundTriangle = i;
					}
				}
			}

			if (foundTriangle >= 0) {
				Triangle minTriangle = GetTriangle (foundTriangle);
				result.material = minTriangle.mat;
				result.normal = GetTriangleNormal (minTriangle, result.position);
			}
			return result;
		}

		bool ShadowIntersection (in Ray ray)
		{
			for (int i = 0; i < TRIANGLE_COUNT; i++) {
				Triangle triangle = GetTriangle (i);
				Intersection currentIntersection;
				if (RayTriangleIntersection (ray, triangle.v0, triangle.v1, triangle.v2, false, currentIntersection)) {
					return true;
				}
			}
			return false;
		}

		vec3 TraceMainRay (in Ray ray)
		{
			vec3 shadedColor = vec3 (0.0, 0.0, 0.0);
			vec3 currentColor = vec3 (1.0, 1.0, 1.0);
			Ray currentRay = ray;
		
			for (int i = 0; i < 3; i++) {
				Intersection intersection = RayModelIntersection (currentRay, true);
				if (intersection.distance == INF) {
					break;
				}

				Material material = GetMaterial (intersection.material);
				if (uPreview == 1) {
					shadedColor = PhongShading (intersection.position, intersection.normal, uLightPosition, material);
					break;
				}
				
				currentColor *= material.diffuse;

				vec3 randomLightPoint = uLightPosition + RandomDirOnSphere (intersection.position.xy) * uLightRadius;
				Ray lightRay;
				vec3 direction = normalize (randomLightPoint - intersection.position);
				lightRay.origin = intersection.position + direction * BIGEPS;
				lightRay.direction = direction;
				lightRay.length = length (randomLightPoint - intersection.position);
				if (!ShadowIntersection (lightRay)) {
					vec3 directLightColor = PhongShading (intersection.position, intersection.normal, randomLightPoint, material);
					shadedColor += currentColor * directLightColor;
				}
				
				currentRay.origin = intersection.position;
				if (Random (intersection.position.xy) < material.reflection) {
					currentRay.direction = GetReflectedDirection (currentRay.direction, intersection.normal);
					currentColor = vec3 (1.0, 1.0, 1.0);
				} else {
					currentRay.direction = RandomDirOnHemisphere (intersection.normal, intersection.position.xy);	
				}
				currentRay.length = INF;
			}
			return shadedColor;
		}

		Ray GetRay (in vec2 vertexPosition, in Camera camera, in float iteration)
		{
			vec3 eyeCenterDir = normalize (camera.center - camera.eye);
			vec3 horizontalDir = normalize (cross (eyeCenterDir, camera.up));
			vec3 verticalDir = normalize (cross (horizontalDir, eyeCenterDir ));

			float fieldOfView = PI / 3.0; // 60 degree
			float cameraDistance = 1.0 / tan (fieldOfView / 2.0);
			vec3 endPosition = camera.eye + eyeCenterDir * cameraDistance;
			
			float randomX = Random (vVertexPosition);
			float randomY = Random (-vVertexPosition);
			float pixelSize = 2.0 / uSize;
			endPosition = endPosition + horizontalDir * (vertexPosition.x + randomX * pixelSize);
			endPosition = endPosition + verticalDir * (vertexPosition.y + randomY * pixelSize);
			
			Ray result;
			result.origin = camera.eye;
			result.direction = normalize (endPosition - camera.eye);
			result.length = INF;
			return result;
		}

		void main (void)
		{
			Camera camera;
			camera.eye = uCameraData[0];
			camera.center = uCameraData[1];
			camera.up = uCameraData[2];
			
			vec2 originalPosition = (vVertexPosition + vec2 (1.0, 1.0)) / 2.0;;
			vec3 originalColor = texture2D (uOriginalTextureSampler, originalPosition).xyz;
			
			Ray ray = GetRay (vVertexPosition, camera, uIteration);
			vec3 shadedColor = TraceMainRay (ray);
			float weight = uIteration / (uIteration + 1.0);
			gl_FragColor = vec4 (mix (shadedColor, originalColor, weight), 1.0);
		}
	</script>
	
	<script type="text/javascript">
		window.onload = function () {
			var canvasElem = document.getElementById ('render');
			var controlsElem = document.getElementById ('controls');
			var fragmentShaderElem = document.getElementById ('fragmentshader');

			var modelTracer = new ModelTracer ();
			modelTracer.Init (canvasElem, controlsElem, fragmentShaderElem);
		}
	</script>

</head>

<body>
	<a href="https://github.com/kovacsv/WebGLPathTrace">
		<img style="position: absolute; top: 0; right: 0; border: 0;" src="https://camo.githubusercontent.com/38ef81f8aca64bb9a64448d0d70f1308ef5341ab/68747470733a2f2f73332e616d617a6f6e6177732e636f6d2f6769746875622f726962626f6e732f666f726b6d655f72696768745f6461726b626c75655f3132313632312e706e67" alt="Fork me on GitHub" data-canonical-src="https://s3.amazonaws.com/github/ribbons/forkme_right_darkblue_121621.png">
	</a>

	<div class="frame">
		<div class="title">General Purpose WebGL Path Tracer</div>
		<div class="text">
			<p>
				...
			</p>
		</div>
		<div class="container">
			<div class="content">
				<div class="canvas">
					<canvas id="render" width="512" height="512"></canvas>
				</div>
			</div>
			<div class="controls" id="controls">
			</div>
		</div>
	</div>
</body>

</html>
