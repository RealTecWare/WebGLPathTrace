<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01//EN"
	 "http://www.w3.org/TR/html4/strict.dtd">

<html>

<head>

	<meta http-equiv="content-type" content="text/html;charset=utf-8">
	<link rel="stylesheet" type="text/css" href="../style/style.css">
	<script type="text/javascript" src="../include/jsmodeler.js"></script>
	<script type="text/javascript" src="../include/gputracer.js"></script>
	<script type="text/javascript" src="shapetracer.js"></script>
	<title>Basic Shape WebGL Path Tracer</title>
	
	<script id="fragmentshader" type="x-shader/fragment">
		precision highp float;

		varying vec2 vVertexPosition;
		uniform float uSize;

		#define EPS 0.00005
		#define BIGEPS 0.005
		#define PI 3.1415926535897932384626433832795
		#define INF 1.0 / 0.0

		uniform sampler2D uOriginalTextureSampler;

		uniform vec3 uCameraData[3];
		uniform vec3 uLightPosition;
		uniform float uLightRadius;
		uniform float uIteration;
		uniform int uPreview;

		struct Triangle
		{
			vec3 v0;
			vec3 v1;
			vec3 v2;
			vec3 n0;
			vec3 n1;
			vec3 n2;
			int mat;
		};

		struct Material
		{
			vec3 diffuse;
			float reflection;
		};

		struct Camera
		{
			vec3 eye;
			vec3 center;
			vec3 up;
		};

		struct Ray
		{
			vec3 origin;
			vec3 direction;
			float length;
		};
		
		struct Sphere
		{
			vec3 origin;
			float radius;
		};
		
		struct Box
		{
			vec3 min;
			vec3 max;
		};

		struct Intersection
		{
			vec3 position;
			float distance;
			vec3 normal;
		};

		#if SPHERE_COUNT > 0
			uniform Sphere uSpheres[SPHERE_COUNT];
		#endif
		#if BOX_COUNT > 0
			uniform Box uBoxes[BOX_COUNT];
		#endif

		float Random (vec2 seed)
		{
			vec2 theSeed = seed + vec2 (uIteration, -uIteration);
			return fract (sin (dot (theSeed, vec2 (12.9898,78.233))) * 43758.5453);
		}

		Intersection RaySphereIntersection (in Ray ray, in Sphere sphere)
		{
			Intersection result;
			result.distance = INF;
			
			vec3 sphereToRay = ray.origin - sphere.origin;
			float a = dot (ray.direction, ray.direction);
			float b = 2.0 * dot (sphereToRay, ray.direction);
			float c = dot (sphereToRay, sphereToRay) - sphere.radius * sphere.radius;

			float disc = b * b - 4.0 * a * c;
			if (disc < EPS) {
				return result;
			}

			float t0 = (-b - sqrt (disc)) * 0.5;
			float t1 = (-b + sqrt (disc)) * 0.5;
			float maxT = max (t0, t1);
			if (maxT < EPS) {
				return result;
			}

			float distance = 0.0;
			float minT = min (t0, t1);
			if (minT < 0.0) {
				distance = maxT;
			} else {
				distance = minT;
			}

			if (distance > ray.length) {
				return result;
			}

			result.position = ray.origin + distance * ray.direction;
			result.distance = distance;
			result.normal = normalize (result.position - sphere.origin);
			return result;
		}

		Intersection RayBoxIntersection (in Ray ray, in Box box)
		{
			Intersection result;
			result.distance = INF;

			int quadrant[3];
			vec3 candidatePlane;

			bool originInBox = true;
			for (int i = 0; i < 3; i++) {
				if (ray.origin[i] < box.min[i]) {
					quadrant[i] = 0;
					candidatePlane[i] = box.min[i];
					originInBox = false;
				} else if (ray.origin[i] > box.max[i]) {
					quadrant[i] = 1;
					candidatePlane[i] = box.max[i];
					originInBox = false;
				} else {
					quadrant[i] = 2;
				}
			}

			if (originInBox) {
				return result;
			}

			vec3 maxT;
			for (int i = 0; i < 3; i++) {
				if (quadrant[i] != 2 && ray.direction[i] != 0.0) {
					maxT[i] = (candidatePlane[i] - ray.origin[i]) / ray.direction[i];
				} else {
					maxT[i] = -1.0;
				}
			}

			int whichPlane = 0;
			float planeDistance = 0.0;
			vec3 normal = vec3 (0.0, 0.0, 0.0);
			if (maxT[0] > maxT[1] && maxT[0] > maxT[2]) {
				whichPlane = 0;
				planeDistance = maxT[0];
				normal[0] = 1.0;
			} else if (maxT[1] > maxT[0] && maxT[1] > maxT[2]) {
				whichPlane = 1;
				planeDistance = maxT[1];
				normal[1] = 1.0;
			} else if (maxT[2] > maxT[0] && maxT[2] > maxT[1]) {
				whichPlane = 2;
				planeDistance = maxT[2];
				normal[2] = 1.0;
			}

			if (planeDistance < 0.0) {
				return result;
			}

			vec3 xCoord;
			for (int i = 0; i < 3; i++) {
				if (whichPlane != i) {
					xCoord[i] = ray.origin[i] + planeDistance * ray.direction[i];
					if (xCoord[i] < box.min[i] || xCoord[i] > box.max[i]) {
						return result;
					}
				} else {
					xCoord[i] = candidatePlane[i];
				}
			}

			float distance = length (ray.origin - xCoord);
			if (distance > ray.length) {
				return result;
			}			
			
			result.position = xCoord;
			result.normal = normalize (normal);
			result.distance = distance;
			return result;
		}

		vec3 GetReflectedDirection (in vec3 direction, in vec3 normal)
		{
			float dotProduct = dot (normal, direction);
			vec3 reflected = direction - normal * 2.0 * dotProduct;
			return normalize (reflected);
		}

		vec3 RandomDirOnSphere (in vec2 seed)
		{
			vec2 random = vec2 (Random (seed), Random (seed * 2.0)) * 6.283;
			vec3 randomDir = vec3 (sin (random.x) * vec2 (sin (random.y), cos (random.y)), cos (random.x));
			return normalize (randomDir);
		}

		vec3 RandomDirOnHemisphere (in vec3 normal, in vec2 seed)
		{
			vec3 randomDir = RandomDirOnSphere (seed);
			return (dot (randomDir, normal) < 0.0) ? -randomDir : randomDir;
		}

		vec3 PhongShading (in vec3 shadedPoint, in vec3 shadedPointNormal, in vec3 lightPosition, in Material shadedMaterial)
		{
			float lightDiffuseIntensity = 1.0;
			vec3 lightDirection = normalize (lightPosition - shadedPoint);
			float diffuseCoeff = max (dot (lightDirection, shadedPointNormal), 0.0);
			vec3 diffuseColor = shadedMaterial.diffuse * lightDiffuseIntensity;
			return diffuseColor * diffuseCoeff;
		}
		
		#define CHECK_INTERSECTION(f, s) Intersection intersection = f (ray, s); if (intersection.distance < result.distance) { result = intersection; }

		Intersection RayModelIntersection (in Ray ray, bool frontFacing)
		{
			Intersection result;
			result.distance = INF;
			#if SPHERE_COUNT > 0
				for (int i = 0; i < SPHERE_COUNT; i++) {
					CHECK_INTERSECTION (RaySphereIntersection, uSpheres[i]);
				}
			#endif
			#if BOX_COUNT > 0
				for (int i = 0; i < BOX_COUNT; i++) {
					CHECK_INTERSECTION (RayBoxIntersection, uBoxes[i]);
				}
			#endif
			return result;
		}

		bool ShadowIntersection (in Ray ray)
		{
			return false;
		}

		vec3 TraceMainRay (in Ray ray)
		{
			vec3 shadedColor = vec3 (0.0, 0.0, 0.0);
			vec3 currentColor = vec3 (1.0, 1.0, 1.0);
			Ray currentRay = ray;
		
			for (int i = 0; i < 5; i++) {
				Intersection intersection = RayModelIntersection (currentRay, true);
				if (intersection.distance == INF) {
					break;
				}

				Material material;
				material.diffuse = vec3 (0.8, 0.0, 0.0);
				material.reflection = 0.0;
				if (uPreview == 1) {
					shadedColor = PhongShading (intersection.position, intersection.normal, uLightPosition, material);
					break;
				}
				
				currentColor *= material.diffuse;

				vec3 randomLightPoint = uLightPosition + RandomDirOnSphere (intersection.position.xy) * uLightRadius;
				Ray lightRay;
				vec3 direction = normalize (randomLightPoint - intersection.position);
				lightRay.origin = intersection.position + direction * BIGEPS;
				lightRay.direction = direction;
				lightRay.length = length (randomLightPoint - intersection.position);
				if (!ShadowIntersection (lightRay)) {
					vec3 directLightColor = PhongShading (intersection.position, intersection.normal, randomLightPoint, material);
					shadedColor += currentColor * directLightColor;
				}
				
				currentRay.origin = intersection.position;
				if (Random (intersection.position.xy) < material.reflection) {
					currentRay.direction = GetReflectedDirection (currentRay.direction, intersection.normal);
					currentColor = vec3 (1.0, 1.0, 1.0);
				} else {
					currentRay.direction = RandomDirOnHemisphere (intersection.normal, intersection.position.xy);	
				}
				currentRay.length = INF;
			}
			return shadedColor;
		}

		Ray GetRay (in vec2 vertexPosition, in Camera camera, in float iteration)
		{
			vec3 eyeCenterDir = normalize (camera.center - camera.eye);
			vec3 horizontalDir = normalize (cross (eyeCenterDir, camera.up));
			vec3 verticalDir = normalize (cross (horizontalDir, eyeCenterDir ));

			float fieldOfView = PI / 3.0; // 60 degree
			float cameraDistance = 1.0 / tan (fieldOfView / 2.0);
			vec3 endPosition = camera.eye + eyeCenterDir * cameraDistance;
			
			float randomX = Random (vVertexPosition);
			float randomY = Random (-vVertexPosition);
			float pixelSize = 2.0 / uSize;
			endPosition = endPosition + horizontalDir * (vertexPosition.x + randomX * pixelSize);
			endPosition = endPosition + verticalDir * (vertexPosition.y + randomY * pixelSize);
			
			Ray result;
			result.origin = camera.eye;
			result.direction = normalize (endPosition - camera.eye);
			result.length = INF;
			return result;
		}

		void main (void)
		{
			Camera camera;
			camera.eye = uCameraData[0];
			camera.center = uCameraData[1];
			camera.up = uCameraData[2];
			
			vec2 originalPosition = (vVertexPosition + vec2 (1.0, 1.0)) / 2.0;;
			vec3 originalColor = texture2D (uOriginalTextureSampler, originalPosition).xyz;
			
			Ray ray = GetRay (vVertexPosition, camera, uIteration);
			vec3 shadedColor = TraceMainRay (ray);
			float weight = uIteration / (uIteration + 1.0);
			gl_FragColor = vec4 (mix (shadedColor, originalColor, weight), 1.0);
		}
	</script>
	
	<script type="text/javascript">
		window.onload = function () {
			var canvasElem = document.getElementById ('render');
			var controlsElem = document.getElementById ('controls');
			var fragmentShaderElem = document.getElementById ('fragmentshader');

			var shapeTracer = new ShapeTracer ();
			shapeTracer.Init (canvasElem, controlsElem, fragmentShaderElem);
		}
	</script>

</head>

<body>
	<a href="https://github.com/kovacsv/WebGLPathTrace">
		<img style="position: absolute; top: 0; right: 0; border: 0;" src="https://camo.githubusercontent.com/38ef81f8aca64bb9a64448d0d70f1308ef5341ab/68747470733a2f2f73332e616d617a6f6e6177732e636f6d2f6769746875622f726962626f6e732f666f726b6d655f72696768745f6461726b626c75655f3132313632312e706e67" alt="Fork me on GitHub" data-canonical-src="https://s3.amazonaws.com/github/ribbons/forkme_right_darkblue_121621.png">
	</a>

	<div class="frame">
		<div class="title">Basic Shape WebGL Path Tracer</div>
		<div class="text">
			<p>
				...
			</p>
		</div>
		<div class="container">
			<div class="canvas">
				<canvas id="render" width="512" height="512"></canvas>
			</div>
			<div class="controls" id="controls">
			</div>
		</div>
	</div>
</body>

</html>
