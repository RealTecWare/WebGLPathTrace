<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01//EN"
	 "http://www.w3.org/TR/html4/strict.dtd">

<html>

<head>

	<meta http-equiv="content-type" content="text/html;charset=utf-8">
	<link rel="stylesheet" type="text/css" href="style/style.css">
	<script type="text/javascript" src="include/jsmodeler.js"></script>
	<script type="text/javascript" src="include/gputracer.js"></script>
	<script type="text/javascript" src="include/userinterface.js"></script>
	<script type="text/javascript" src="include/distancefield.js"></script>
	<title>Solid Operations with Distance Field Path Tracing</title>
	
	<script id="fragmentshader" type="x-shader/fragment">
		precision highp float;

		varying vec2 vVertexPosition;
		uniform float uSize;

		#define EPS 0.000005
		#define BIGEPS 0.005
		#define PI 3.1415926535897932384626433832795
		#define INF 10e10

		uniform sampler2D uOriginalTextureSampler;
		uniform vec3 uCameraData[3];
		uniform vec3 uLightPosition;
		uniform float uLightRadius;
		uniform float uIteration;
		uniform float uShapeData1[6];
		uniform float uShapeData2[6];
		uniform int uPreview;

		struct Camera
		{
			vec3 eye;
			vec3 center;
			vec3 up;
		};

		struct Ray
		{
			vec3 origin;
			vec3 direction;
			float length;
		};

		struct Intersection
		{
			vec3 position;
			float distance;
			vec3 color;
			bool fog;
		};

		struct Estimation
		{
			float distance;
			vec3 color;
			bool fog;
		};
		
		float Random (vec2 seed)
		{
			vec2 theSeed = seed + vec2 (uIteration, -uIteration);
			return fract (sin (dot (theSeed, vec2 (12.9898,78.233))) * 43758.5453);
		}

		vec3 Shading (in vec3 shadedPoint, in vec3 shadedPointNormal, in vec3 lightPosition, in vec3 materialDiffuseColor)
		{
			float lightDiffuseIntensity = 1.0;
			vec3 lightDirection = normalize (lightPosition - shadedPoint);
			float diffuseCoeff = max (dot (lightDirection, shadedPointNormal), 0.0);
			vec3 diffuseColor = materialDiffuseColor * lightDiffuseIntensity;
			return diffuseColor * diffuseCoeff;
		}

		float PointBoxDistance (vec3 point, vec3 origin, vec3 size)
		{
			vec3 offseted = point - origin;
			vec3 d = abs (offseted) - size;
			return min (max (d.x, max (d.y, d.z)), 0.0) + length (max (d, 0.0));
		}

		float PointSphereDistance (in vec3 point, in vec3 origin, in float radius)
		{
			vec3 offseted = point - origin;
			return length (offseted) - radius;
		}

		float PointCylinderDistance (in vec3 point, in vec3 origin, in float radius, in float height)
		{
			vec3 offseted = point - origin;
			float distance = length (offseted.xy) - radius;
			distance = max (distance, -offseted.z - height);
			distance = max (distance, offseted.z - height);
			return distance;
		}
	
		float PointTorusDistance (in vec3 point, in vec3 origin, float outerRadius, float innerRadius)
		{
			vec3 offseted = point - origin;
			return length (vec2 (length (offseted.xy) - outerRadius, offseted.z)) - innerRadius;
		}
	
		Estimation DistanceEstimation (in vec3 point)
		{
			float planeDistance = point.z + 0.0;
			float shape1Distance = INF;
			float shape2Distance = INF;
			#if SHAPETYPE1 == 1
				shape1Distance = PointSphereDistance (point, vec3 (uShapeData1[0], uShapeData1[1], uShapeData1[2]), uShapeData1[3]);
			#elif SHAPETYPE1 == 2
				shape1Distance = PointBoxDistance (point, vec3 (uShapeData1[0], uShapeData1[1], uShapeData1[2]), vec3 (uShapeData1[3], uShapeData1[4], uShapeData1[5]));
			#elif SHAPETYPE1 == 3
				shape1Distance = PointCylinderDistance (point, vec3 (uShapeData1[0], uShapeData1[1], uShapeData1[2]), uShapeData1[3], uShapeData1[4]);
			#elif SHAPETYPE1 == 4
				shape1Distance = PointTorusDistance (point, vec3 (uShapeData1[0], uShapeData1[1], uShapeData1[2]), uShapeData1[3], uShapeData1[4]);
			#endif
			#if SHAPETYPE2 == 1
				shape2Distance = PointSphereDistance (point, vec3 (uShapeData2[0], uShapeData2[1], uShapeData2[2]), uShapeData2[3]);
			#elif SHAPETYPE2 == 2
				shape2Distance = PointBoxDistance (point, vec3 (uShapeData2[0], uShapeData2[1], uShapeData2[2]), vec3 (uShapeData2[3], uShapeData2[4], uShapeData2[5]));
			#elif SHAPETYPE2 == 3
				shape2Distance = PointCylinderDistance (point, vec3 (uShapeData2[0], uShapeData2[1], uShapeData2[2]), uShapeData2[3], uShapeData2[4]);
			#elif SHAPETYPE2 == 4
				shape2Distance = PointTorusDistance (point, vec3 (uShapeData2[0], uShapeData2[1], uShapeData2[2]), uShapeData2[3], uShapeData2[4]);
			#endif
			
			float operationDistance = INF;
			#if OPERATION == 1
				operationDistance = min (shape1Distance, shape2Distance);
			#elif OPERATION == 2
				operationDistance = max (shape1Distance, -shape2Distance);
				shape1Distance *= -1.0;
			#elif OPERATION == 3
				operationDistance = max (shape1Distance, shape2Distance);
			#endif
			
			Estimation result;
			result.distance = min (planeDistance, operationDistance);
			result.fog = false;
			if (planeDistance < operationDistance) {
				result.color = vec3 (0.4, 0.4, 0.4);
				result.fog = true;
			} else {
				if (shape1Distance < shape2Distance) {
					result.color = vec3 (0.8, 0.0, 0.0);
				} else {
					result.color = vec3 (0.0, 0.8, 0.0);
				}
			}
			return result;
		}

		vec3 CalculateNormal (in vec3 point)
		{
			vec3 xDir = vec3 (EPS, 0.0, 0.0);
			vec3 yDir = vec3 (0.0, EPS, 0.0);
			vec3 zDir = vec3 (0.0, 0.0, EPS);
			vec3 normal = vec3 (
				DistanceEstimation (point + xDir).distance - DistanceEstimation (point - xDir).distance,
				DistanceEstimation (point + yDir).distance - DistanceEstimation (point - yDir).distance,
				DistanceEstimation (point + zDir).distance - DistanceEstimation (point - zDir).distance
			);
			return normalize (normal);
		}

		Intersection RayModelIntersection (in Ray ray)
		{
			Intersection result;
			result.distance = INF;
			const int maxSteps = 150;
			const float maxDistance = 50.0;
			
			vec3 shadedColor = vec3 (0.0, 0.0, 0.0);
			float distance = 0.0;
			for (int i = 0; i <= maxSteps; i++) {
				vec3 currentPoint = ray.origin + ray.direction * distance;
				Estimation estimation = DistanceEstimation (currentPoint);
				distance += estimation.distance;
				result.position = currentPoint;
				result.distance = distance;
				result.color = estimation.color;
				result.fog = estimation.fog;
				if (estimation.distance < EPS || distance > maxDistance) {
					break;
				}

			}
			if (distance > maxDistance) {
				result.distance = INF;
			}
			return result;
		}

		vec3 GetReflectedDirection (in vec3 direction, in vec3 normal)
		{
			float dotProduct = dot (normal, direction);
			vec3 reflected = direction - normal * 2.0 * dotProduct;
			return normalize (reflected);
		}

		vec3 RandomDirOnSphere (in vec2 seed)
		{
			vec2 random = vec2 (Random (seed), Random (seed * 2.0)) * 6.283;
			vec3 randomDir = vec3 (sin (random.x) * vec2 (sin (random.y), cos (random.y)), cos (random.x));
			return normalize (randomDir);
		}

		vec3 RandomDirOnHemisphere (in vec3 normal, in vec2 seed)
		{
			vec3 randomDir = RandomDirOnSphere (seed);
			return (dot (randomDir, normal) < 0.0) ? -randomDir : randomDir;
		}

		vec3 TraceRay (in Ray ray, in Camera camera)
		{
			vec3 backgroundColor = vec3 (0.8, 0.8, 0.8);
			
			vec3 shadedColor = vec3 (0.0, 0.0, 0.0);
			vec3 currentColor = vec3 (1.0, 1.0, 1.0);
			Ray currentRay = ray;
			for (int i = 0; i < 3; i++) {
				Intersection intersection = RayModelIntersection (currentRay);
				if (intersection.distance == INF) {
					if (i == 0) {
						shadedColor = backgroundColor;
					}
					break;
				}

				vec3 intersectionNormal = CalculateNormal (intersection.position);
				currentColor *= intersection.color;

				Ray lightRay;
				vec3 randomLightPoint = uLightPosition + RandomDirOnSphere (intersection.position.xy) * uLightRadius;
				vec3 lightRayDirection = normalize (randomLightPoint - intersection.position);
				lightRay.origin = intersection.position + lightRayDirection * BIGEPS;
				lightRay.direction = lightRayDirection;
				lightRay.length = length (randomLightPoint - intersection.position);
				Intersection lightIntersection = RayModelIntersection (lightRay);
				if (lightIntersection.distance == INF) {
					vec3 directLightColor = Shading (intersection.position, intersectionNormal, randomLightPoint, intersection.color);
					shadedColor += currentColor * directLightColor;
					if (i == 0 && intersection.fog) {
						float fog = 1.0 - exp (-intersection.distance * 0.2);
						shadedColor = mix (shadedColor, backgroundColor, fog);
					}
				}

				if (uPreview == 1) {
					break;
				}
				
				vec3 newDirection = RandomDirOnHemisphere (intersectionNormal, intersection.position.xy);
				currentRay.origin = intersection.position + newDirection * BIGEPS;
				currentRay.direction = newDirection;
				currentRay.length = INF;
			}
			return shadedColor;
		}

		Ray GetRay (in vec2 vertexPosition, in Camera camera, in float iteration)
		{
			vec3 eyeCenterDir = normalize (camera.center - camera.eye);
			vec3 horizontalDir = normalize (cross (eyeCenterDir, camera.up));
			vec3 verticalDir = normalize (cross (horizontalDir, eyeCenterDir ));

			float fieldOfView = PI / 3.0; // 60 degree
			float cameraDistance = 1.0 / tan (fieldOfView / 2.0);
			vec3 endPosition = camera.eye + eyeCenterDir * cameraDistance;
			
			float randomX = Random (vVertexPosition);
			float randomY = Random (-vVertexPosition);
			float pixelSize = 2.0 / uSize;
			endPosition = endPosition + horizontalDir * (vertexPosition.x + randomX * pixelSize);
			endPosition = endPosition + verticalDir * (vertexPosition.y + randomY * pixelSize);
			
			Ray result;
			result.origin = camera.eye;
			result.direction = normalize (endPosition - camera.eye);
			result.length = INF;
			return result;
		}

		void main (void)
		{
			Camera camera;
			camera.eye = uCameraData[0];
			camera.center = uCameraData[1];
			camera.up = uCameraData[2];
			
			Ray ray = GetRay (vVertexPosition, camera, uIteration);
			vec3 shadedColor = TraceRay (ray, camera);
			
			vec2 originalPosition = (vVertexPosition + vec2 (1.0, 1.0)) / 2.0;;
			vec3 originalColor = texture2D (uOriginalTextureSampler, originalPosition).xyz;
			float weight = uIteration / (uIteration + 1.0);
			gl_FragColor = vec4 (mix (shadedColor, originalColor, weight), 1.0);
		}
	</script>
	
	<script type="text/javascript">
		window.onload = function () {
			var canvasElem = document.getElementById ('render');
			var controlsElem = document.getElementById ('controls');
			var fragmentShaderElem = document.getElementById ('fragmentshader');

			var distanceField = new DistanceField ();
			distanceField.Init (canvasElem, controlsElem, fragmentShaderElem);
		}
	</script>
	
</head>

<body>
	<a href="https://github.com/kovacsv/WebGLPathTrace">
		<img style="position: absolute; top: 0; right: 0; border: 0;" src="https://camo.githubusercontent.com/38ef81f8aca64bb9a64448d0d70f1308ef5341ab/68747470733a2f2f73332e616d617a6f6e6177732e636f6d2f6769746875622f726962626f6e732f666f726b6d655f72696768745f6461726b626c75655f3132313632312e706e67" alt="Fork me on GitHub" data-canonical-src="https://s3.amazonaws.com/github/ribbons/forkme_right_darkblue_121621.png">
	</a>

	<div class="frame">
		<div class="title">Solid Operations with Distance Field Path Tracing</div>
		<div class="text">
			<p>
				Distance Field Path Tracing is a ray marching technique where we don't find the exact intersection between
				the model and the ray, we only estimate which is the nearest object along the ray. For this method we need to
				define a signed distance function for all shapes, and with these we can calculate solid operations very easily.
				The implementation is based on <a href="http://blog.hvidtfeldts.net/index.php/2011/06/distance-estimated-3d-fractals-part-i">this great article</a>,
				camera handling and other calculations made by <a href="https://github.com/kovacsv/JSModeler">jsmodeler</a>.
			</p>
		</div>
		<div class="container">
			<div class="canvas">
				<canvas id="render" width="512" height="512"></canvas>
			</div>
			<div class="controls" id="controls">
			</div>
		</div>
	</div>
</body>

</html>
